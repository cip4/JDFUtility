import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

group = 'org.cip4.tools.jdfutility'
description = 'CIP4 JDF Utilities'

if (project.hasProperty('projectVersion')) {
    version = project.projectVersion
} else {
    version = '1.0-SNAPSHOT'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    systemProperty "file.encoding", "utf-8"
    maxParallelForks = 1
    forkEvery = 1
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name

            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.description
                description = 'CIP4 JDF Utilities'
                url = 'https://github.com/cip4/JDFUtility'

                licenses {
                    license {
                        name = 'CIP4 Software License'
                        url = 'https://github.com/cip4/JDFUtility/blob/master/LICENSE.md'
                    }
                }
                scm {
                    connection = 'https://github.com/cip4/JDFUtility.git'
                    developerConnection = 'scm:git:https://github.com/cip4/JDFUtility.git'
                    url = 'https://github.com/cip4/JDFUtility'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

nexusStaging {
    packageGroup = 'org.cip4'
    stagingProfileId = '105e2b532689c6'
    numberOfRetries = 400
    delayBetweenRetriesInMillis = 2500
    username ossrhUsername
    password ossrhPassword
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

signing {
    if (!version.endsWith("SNAPSHOT")) {
        sign publishing.publications.mavenJava
    }
}

javadoc {
    failOnError = false
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

processResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens:  [
                'name': project.properties['description'],
                'version': project.properties['version'],
                'artifactId': project.properties['name'],
                'timestamp': new Date().format('yyyy-MM-dd HH:mm:ssZ')
        ])
    }
}

dependencies {
    implementation 'org.cip4.lib.jdf:JDFLibJ:2.1.7.+'
    implementation 'org.eclipse.jetty:jetty-servlet:9.4.24.v20191120'
    implementation 'commons-fileupload:commons-fileupload:1.3.3'

    testImplementation 'junit:junit:4.13.2'
}