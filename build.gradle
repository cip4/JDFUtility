import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'jacoco'
}

group = 'org.cip4.tools.jdfutility'
description = 'CIP4 JDF Utilities'
version = project.hasProperty('projectVersion') ? project.projectVersion : '1.0-SNAPSHOT'

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name

            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = project.description
                description = 'CIP4 JDF Utilities'
                url = 'https://github.com/cip4/JDFUtility'

                organization {
                    name = "CIP4 Organization"
                    url = "https://www.cip4.org"
                }
                developers {
                    developer {
                        id = "rainer-prosi"
                        name = "Dr. Rainer Prosi"
                    }
                }
                licenses {
                    license {
                        name = 'CIP4 Software License'
                        url = 'https://github.com/cip4/JDFUtility/blob/master/LICENSE.md'
                    }
                }
                scm {
                    connection = 'https://github.com/cip4/JDFUtility.git'
                    developerConnection = 'scm:git:https://github.com/cip4/JDFUtility.git'
                    url = 'https://github.com/cip4/JDFUtility'
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"
            def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://central.sonatype.com/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = ossrhUsername
            password = ossrhPassword
            stagingProfileId = '105e2b532689c6' // This can often be omitted if your packageGroup is unique
            packageGroup = 'org.cip4'
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

signing {
    if (!version.endsWith("SNAPSHOT")) {
        sign publishing.publications.mavenJava
    }
}

javadoc {
    failOnError = false
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

processResources {
    filesMatching(['**/*.properties']) {
        filter(ReplaceTokens, tokens: [
                'name': project.properties['description'],
                'version': project.properties['version'],
                'artifactId': project.properties['name'],
                'timestamp': new Date().format('yyyy-MM-dd HH:mm:ssZ')
        ])
    }
}

dependencies {
    implementation('org.cip4.lib.jdf:JDFLibJ:2.+') {
        exclude group: 'xml-apis'
    }

    implementation 'org.eclipse.jetty:jetty-server:12.0.21'

    implementation 'org.eclipse.jetty.ee10:jetty-ee10-servlet:12.0.22'
    implementation("org.apache.commons:commons-lang3:3.20.0")
    implementation 'org.apache.commons:commons-text:1.11.0'
    implementation 'org.apache.commons:commons-fileupload2-jakarta-servlet6:2.+'
    implementation 'jakarta.servlet:jakarta.servlet-api:6.1.0'  
    implementation 	'com.sun.mail:jakarta.mail:2.0.1'

    
    implementation 'org.apache.logging.log4j:log4j-core:2.24.1'
    implementation 'org.apache.logging.log4j:log4j-jcl:2.25.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
    testImplementation 'org.springframework:spring-test:6.2.+'
    testImplementation 'org.springframework:spring-web:6.2.+'
    testImplementation 'org.mockito:mockito-core:5.17.0'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = false
    }
}

test {
    useJUnitPlatform()
    systemProperty "file.encoding", "utf-8"
    maxParallelForks = 1
    forkEvery = 1
}

jar {
    manifest {
        attributes(
                "Implementation-Build-Date": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Implementation-Title": project.description,
                "Implementation-Version": project.version,
                "Implementation-Vendor-Id": project.group,
                "Specification-Title": project.description,
                "Specification-Version": project.version,
                "Build-Jdk": JavaVersion.current(),
        )
    }
}

tasks.register('fatJar', Jar) {
    manifest {
        attributes(
                "Main-Class": "org.cip4.jdfutility.exe.CheckJDFServer"
        )
    }
    archiveAppendix = "fat"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

tasks.withType(Test).configureEach {
    testLogging {
        events TestLogEvent.FAILED,
               TestLogEvent.SKIPPED
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
    }
}
